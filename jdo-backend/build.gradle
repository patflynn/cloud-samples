buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.1'
    }
}

apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine'

war{
    version = '1.0'
}

configurations {
    jdo {
        extendsFrom compile
    }
}

dependencies {
    compile 'com.google.appengine:appengine-api-1.0-sdk:1.9.51'
    compile 'javax.servlet:servlet-api:2.5'
    compile 'jstl:jstl:1.2'
    compile 'javax.jdo:jdo-api:3.0.1'
    compile 'org.datanucleus:datanucleus-core:3.1.4'
    compile 'org.datanucleus:datanucleus-api-jdo:3.1.3'
    compile 'com.google.appengine.orm:datanucleus-appengine:2.1.2'
    jdo 'org.datanucleus:datanucleus-enhancer:3.1.1'
}

task jdoEnhance {
    description "Enhance JPA model classes using DataNucleus Enhancer"

    doLast {
        // define the entity classes
        def entityFiles = fileTree(sourceSets.main.output.classesDir).matching {
            include 'com/google/**/*.class'
        }

        println "Enhancing with DataNucleus the following files"
        entityFiles.getFiles().each {
            println it
        }

        // define Ant task for DataNucleus Enhancer
        ant.taskdef(
                name : 'enhancer',
                classpath : configurations.jdo.asPath,
                classname : 'org.datanucleus.enhancer.tools.EnhancerTask'
        )

        // run the DataNucleus Enhancer as an Ant task
        ant.enhancer(
                classpath: sourceSets.main.runtimeClasspath.asPath,
                verbose: true,
                api: "JDO") {
            entityFiles.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
        }
    }
}

classes.dependsOn jdoEnhance

appengine {
    deploy {
        version = '1'
        project = appengineProject
    }
}