buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.0.0'
    }
}

apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine'

war{
    version = '1.0'
}

configurations {
    jdo {
        extendsFrom compile
    }
}

dependencies {
    compile 'com.google.appengine:appengine-api-1.0-sdk:1.9.48'
    compile 'javax.servlet:servlet-api:2.5'
    compile 'jstl:jstl:1.2'
    compile 'javax.jdo:jdo-api:3.0.1'
    compile 'org.datanucleus:datanucleus-core:3.1.4'
    compile 'org.datanucleus:datanucleus-api-jdo:3.1.3'
    compile 'com.google.appengine.orm:datanucleus-appengine:2.1.2'
    jdo 'org.datanucleus:datanucleus-enhancer:3.1.1'
}

// Borrowed from https://issues.gradle.org/browse/GRADLE-1375
task jdoEnhance << {
    ant.taskdef(name: 'enhancer', classname: 'org.datanucleus.enhancer.tools.EnhancerTask', classpath: configurations.jdo.asPath)
    ant.enhancer( dir: sourceSets.main.output.classesDir.canonicalPath.toURI().toString(), verbose: 'true') {
        classpath {
            pathelement(location: sourceSets.main.output.classesDir.canonicalPath.toURI().toString())
            pathelement(path: configurations.jdo.asPath)
        }
        fileset(dir: sourceSets.main.output.classesDir.canonicalPath.toURI().toString()) {
            include(name: '**/*.class')
        }
    }
}

classes.dependsOn jdoEnhance

appengine {
    deploy {
        version = '1'
        project = appengineProject
    }
}